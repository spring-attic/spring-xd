apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

// exclude hadoop install sub projects
def hadoopInstalls=['cdh5','hadoop22','hadoop24','hdp21','phd1','phd20']
if (hadoopInstalls.contains(project.name)) {
	// Disable installing hadoop distro artifacts
	install.enabled = false
	if (project.plugins.hasPlugin('artifactory-publish')) {
		// Disable publishing hadoop distro artifacts
		artifactoryPublish.enabled = false
	}
}

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = linkHomepage
			organization {
				name = 'Spring by Pivotal'
				url = 'https://spring.io'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = linkScmUrl
				connection = linkScmConnection
				developerConnection = linkScmDevConnection
			}

			repositories {
			        repository {
					id = 'spring-snapshots'
 					name = 'Spring Snapshots'
					url = 'http://repo.spring.io/snapshot'
					snapshots {
					        enabled = 'true'
					}
				}
			}

			dependencyManagement {
				dependencies {
					dependency {
						groupId = 'io.spring.platform'
						artifactId = 'platform-bom'
						version = platformVersion
						scope = 'import'
						type = 'pom'
					}
				}
			}

			developers {
				developer {
					id = 'markfisher'
					name = 'Mark Fisher'
					email = 'mfisher@gopivotal.com'
				}
				developer {
					id = 'markpollack'
					name = 'Mark Pollack'
					email = 'mpollack@gopivotal.com'
				}
				developer {
					id = 'garyrussell'
					name = 'Gary Russell'
					email = 'grussell@gopivotal.com'
				}
				developer {
					id = 'ghillert'
					name = 'Gunnar Hillert'
					email = 'ghillert@gopivotal.com'
				}
			}
		}
	}
}
